{
  parserClass="org.mvnsearch.plugins.wit.lang.parser.WitParser"
  extends="com.intellij.extapi.psi.ASTWrapperPsiElement"

  psiClassPrefix="Wit"
  psiImplClassSuffix="Impl"
  psiPackage="org.mvnsearch.plugins.wit.lang.psi"
  psiImplPackage="org.mvnsearch.plugins.wit.lang.psi.impl"

  elementTypeHolderClass="org.mvnsearch.plugins.wit.lang.psi.WitTypes"
  elementTypeClass="org.mvnsearch.plugins.wit.lang.psi.WitElementType"
  tokenTypeClass="org.mvnsearch.plugins.wit.lang.psi.WitTokenType"

  psiImplUtilClass="org.mvnsearch.plugins.wit.lang.psi.impl.WitPsiImplUtil"


  tokens=[
              LBRACE      =  '{'
              RBRACE      =  '}'
              LBRACK      =  '['
              RBRACK      =  ']'
              LPAREN      =  '('
              RPAREN      =  ')'
              COLON       =  ':'
              COMMA       =  ','
              SEMICOLON   =  ';'
              EQ          =  '='
              EXCL        =  '!'
              EXCLEQ      =  '!='
              EQEQ        =  '=='
              PLUS        =  '+'
              MINUS       =  '-'
              MUL         =  '*'
              DIV         =  '/'
              REM         =  '%'
              OR          =  '|'
              LT          =  '<'
              LT_EQ       =  '<='
              GT          =  '>'
              GT_EQ       =  '>='
              DOT         =  '.'
              DOTDOT      =  '..'
              ARROW       =  '->'
              AT          =  '@'
              DOLLAR      =  '$'
              WHITE_SPACE = 'regexp:[ \t\r\n]+'
              IDENTIFIER = 'regexp:[a-zA-Z][a-zA-Z0-9-]*'
              COMMENT = 'regexp:("//")[^\n]*'
              DOC_COMMENT = 'regexp:("///")[^\n]*'
              BLOCK_COMMENT_START = '/*'
              BLOCK_COMMENT_END = '*/'
              INTEGER_LITERAL = 'regexp:\d+'
              DOUBLE_LITERAL = 'regexp:(\d[\d_]*)(\.)(\d[\d_]*)'
              CHAR_LITERAL = "regexp:([^\r\n \t]*)"
              STRING_LITERAL = 'regexp:("[^\\"\r\n]*")'
              BUILTIN_TYPE = 'builtin_type'
              PATH_PREFIX_KEYWORD = 'regexp:(pkg|self)'
              TYPE_DECLARE_KEYWORD = 'regexp:(resource|union|flags|record|enum|variant)'
              REFER_KEYWORD = 'regexp:(use|import|export)'
              DEFAULT_KEYWORD = 'default'
              FUNC_KEYWORD = 'func'
              INTERFACE_KEYWORD = 'interface'
              WORLD_KEYWORD = 'world'
              PACKAGE_KEYWORD = 'package'
              RESERVED_KEYWORD = 'regexp:(as|static|future|stream)'
              SEMANTIC_VERSION = 'regexp:([0-9]+)\.([0-9]+)\.([0-9]+)([0-9A-Za-z-]+(\.[0-9A-Za-z-]+)*)?(\+[0-9A-Za-z-]+)?'
          ]
}



WitDocument ::= any*

private recover ::= !(any)
private any ::= !<<eof>> ( interface-item | world-item | package-item | NEW_LINE | DOC_COMMENT | COMMENT | comment-block ) {pin=1 recoverWhile=recover}

any-keyword ::= BUILTIN_TYPE | TYPE_DECLARE_KEYWORD | REFER_KEYWORD | DEFAULT_KEYWORD | FUNC_KEYWORD | INTERFACE_KEYWORD | WORLD_KEYWORD | PATH_PREFIX_KEYWORD | RESERVED_KEYWORD
comment-block ::= BLOCK_COMMENT_START (WHITE_SPACE | CHAR_LITERAL)* BLOCK_COMMENT_END

// types
ty ::= BUILTIN_TYPE | IDENTIFIER
tuple ::= 'tuple' '<' tuple-list '>'
tuple-list ::= tuple-type (',' tuple-type)* ','?
tuple-type ::= list|tuple|ty
list ::= 'list' '<' list-type '>'
list-type ::= tuple|list|option|ty
option ::= 'option' '<' option-type '>'
option-type ::= option | list | tuple | result | ty
result ::= 'result' '<' result-type ',' result-type '>' | 'result' '<' '_' ',' result-type '>' | 'result' '<' result-type '>' | 'result'
result-type ::= tuple|list|option|ty
wit-type ::= tuple | list | option | result | '%' BUILTIN_TYPE | ty

// package item
package-item ::= 'package' package-namespace SEMANTIC_VERSION? SEMICOLON
package-namespace ::= package-namespace-item (':' package-namespace-item)*
package-namespace-item ::= IDENTIFIER

// world item
world-item ::= 'default'? 'world' world-name '{' world-items*  '}' {pin=3}
world-name ::= IDENTIFIER
world-items ::= world-export-item | world-import-item  | typedef-item | use-item | include-item
world-export-item ::= 'export' (world-export-item-refer | world-export-item-function | world-export-item-interface )
world-export-item-name ::= IDENTIFIER
world-export-item-refer ::= world-export-item-name SEMICOLON
world-export-item-function ::= IDENTIFIER ':' func-type SEMICOLON
world-export-item-interface ::= IDENTIFIER ':' world-interface-type
world-import-item ::= 'import' (world-import-item-refer | world-import-item-function | world-import-item-interface)
world-import-item-name ::= IDENTIFIER (':' IDENTIFIER '/' IDENTIFIER)?
world-import-item-refer ::= world-import-item-name SEMICOLON
world-import-item-function ::= IDENTIFIER ':' func-type SEMICOLON
world-import-item-interface ::= IDENTIFIER ':' world-interface-type
world-interface-type ::= 'interface' '{' interface-items* '}'

// use item
use-item ::= 'use' use-path '.' '{' use-names-list? '}' SEMICOLON
use-names-list ::= use-names-type (',' use-names-type)*
use-names-type ::= use-names-type-name 'as' use-names-type-alias | use-names-type-name
use-names-type-name ::= IDENTIFIER
use-names-type-alias ::= IDENTIFIER | '%' BUILTIN_TYPE
// use path
use-path ::= use-path-pkg |use-path-self| use-path-package SEMICOLON
use-path-pkg-name ::= IDENTIFIER
use-path-pkg ::=  use-path-pkg-name (':' use-path-pkg-wit-name '/' use-path-pkg-interface-name)? ('.' use-path-part)?
use-path-pkg-wit-name ::= IDENTIFIER
use-path-pkg-interface-name ::= IDENTIFIER
use-path-self ::= use-path-self-interface-name ('.' use-path-part)? {pin=3}
use-path-self-interface-name ::= IDENTIFIER
use-path-package ::= use-path-package-name '.' use-path-package-document-name ('.' use-path-package-interface-name)? ('.' use-path-part)?
use-path-package-name ::= IDENTIFIER
use-path-package-document-name ::= IDENTIFIER
use-path-package-interface-name ::= IDENTIFIER
use-path-part ::= IDENTIFIER

// include item
include-item ::= 'include' use-path | 'include' use-path 'with' '{' include-names-list '}' SEMICOLON
include-names-list ::= include-names-type (',' include-names-type)*
include-names-type ::= include-names-type-name 'as' include-names-type-alias | include-names-type-name
include-names-type-name ::= IDENTIFIER
include-names-type-alias ::= IDENTIFIER | '%' BUILTIN_TYPE

// interface item
interface-item ::= 'default'? 'interface' interface-name '{' interface-items* '}' { pin= 3 mixin="org.mvnsearch.plugins.wit.lang.psi.WitInterfaceItemElementImpl" implements="org.mvnsearch.plugins.wit.lang.psi.WitNamedElement" methods=[getKey getValue getName setName getNameIdentifier getPresentation] }
interface-name ::= IDENTIFIER
interface-items ::= typedef-item | use-item | func-item

// typedef item
typedef-item ::= resource-item | variant-item | record-item | union-item | flags-item | enum-item | type-item

// type item
type-item ::= 'type' type-item-name '=' wit-type SEMICOLON  {pin=2}
type-item-name ::= IDENTIFIER

// flags item
flags-item ::= 'flags' flags-item-name '{' flags-fields '}' {pin=2}
flags-item-name ::= IDENTIFIER
flags-fields ::= flags-field-name (',' flags-field-name)* ','?
flags-field-name ::= IDENTIFIER

// variant item
variant-item ::= 'variant' variant-item-name '{' variant-cases '}' {pin=2}
variant-item-name ::= IDENTIFIER
variant-cases ::= variant-case (',' variant-case)* ','?
variant-case ::=  variant-case-name '(' wit-type ')' | variant-case-name
variant-case-name ::= IDENTIFIER

// enum item
enum-item ::= 'enum' enum-item-name '{' enum-cases '}' {pin=2}
enum-item-name ::= IDENTIFIER
enum-cases ::= enum-case-name (',' NEW_LINE* enum-case-name)* ','?
enum-case-name ::= IDENTIFIER

// union item
union-item ::= 'union' union-item-name '{' union-cases '}' {pin=2}
union-item-name ::= IDENTIFIER
union-cases ::= union-case-type (',' union-case-type)* ','?
union-case-type ::= wit-type

// resource item
resource-item ::= 'resource' resource-item-name '{' resource-func-item* '}' {pin=2}
resource-item-name ::= IDENTIFIER
resource-func-item ::= func-item

// record item
record-item ::= 'record' record-item-name '{' record-fields? '}' {pin=2}
record-item-name ::= IDENTIFIER
record-fields ::= record-field (',' record-field)* ','?
record-field ::= record-field-name ':' wit-type
record-field-name ::= '%'? (IDENTIFIER | any-keyword)

// function item
func-item ::= func-name ':' func-type SEMICOLON
func-name ::= '%'? (IDENTIFIER | any-keyword)
func-type ::= 'func' '(' func-param-list? ')' func-result?
func-param-list ::=  func-param ( ',' func-param )* ','?
func-param ::= func-param-name ':' wit-type
func-param-name ::= '%'? (IDENTIFIER | any-keyword)
func-result ::=  '->' wit-type | '->' '(' func-result-list? ')'
func-result-list ::= func-result-named-pair ( ',' func-result-named-pair )*
func-result-named-pair ::= func-result-named-pair-name ':' wit-type
func-result-named-pair-name ::= '%'? (IDENTIFIER | any-keyword)



